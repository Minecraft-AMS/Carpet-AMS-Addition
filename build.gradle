plugins {
	id 'maven-publish'
	// https://www.fabricmc.net/develop/
	id 'fabric-loom' version '1.2-SNAPSHOT' apply false
	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'e1050f4d72'
}

preprocess {
	def mc116 = createNode('1.16.5', 1_16_05, 'yarn')
	def mc117 = createNode('1.17.1', 1_17_01, 'yarn')
	def mc118 = createNode('1.18.2', 1_18_02, 'yarn')
	def mc119 = createNode('1.19.4', 1_19_04, 'yarn')
	def mc1200 = createNode('1.20', 1_20_00, 'yarn')
	def mc1201 = createNode('1.20.1', 1_20_00, 'yarn')

	mc117.link(mc116, file('versions/mapping-1.17-1.16.txt'))
	mc118.link(mc117, file('versions/mapping-1.18-1.17.txt'))
	mc118.link(mc119, file('versions/mapping-1.18-1.19.txt'))
	mc119.link(mc1200, file('versions/mapping-1.19-1.20.txt'))
	mc119.link(mc1201, file('versions/mapping-1.19-1.20.txt'))
}

task buildAndGather {
	subprojects {
		dependsOn project.name + ':build'
	}
	doFirst {
		println 'Gathering builds'
		copy {
			subprojects {
				def libDir = project.projectDir.toPath().resolve('build/libs')
				from(libDir) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into 'build/libs/'
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}